apply plugin: 'com.android.library'
import org.apache.tools.ant.taskdefs.condition.Os

// VSee integration:
//
// First repository is for VSee released artifacts.  Others pick up transitive dependencies.
// To access the VSee bintray repo, you must create a bintray account (it is free) and have
// us add you to our team.
//
repositories {
    maven {
        credentials {
            username bintrayUsername
            password bintrayApiKey
        }
        url 'http://dl.bintray.com/vsee/android'
    }
    maven {
        url "https://repo.commonsware.com.s3.amazonaws.com"
    }
    maven {
        url "http://dl.bintray.com/populov/maven"
    }
    jcenter()
}

android {
    compileSdkVersion 21
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled true
        }
    }
    // increase GC memory allocation threshold to prevent compile/dex-time errors
    dexOptions {
        javaMaxHeapSize "3g"
    }

    lintOptions {
        abortOnError false
    }

    // add a task that calls regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine 'ndk-build.cmd', '-C', file('src\\main').absolutePath
        } else {
            commandLine 'ndk-build', '-C', file('src\\main').absolutePath
        }
    }

    // add this task as a dependency of Java compilation
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
}

// VSee integration:
// If you don't include this section you will get errors due to duplicate files in
// 3rd party dependencies.
android.packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'version.properties'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // VSee integration:
    compile('com.vsee:vsee-kit:0.3.20524@aar') {
        transitive = true
    }
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support:support-v13:22.1.1'
    compile 'com.loopj.android:android-async-http:1.4.3'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:recyclerview-v7:22.1.1'
    compile 'com.google.android.gms:play-services-maps:6.5.87'
    compile files('libs/volley.jar')
    compile 'com.google.code.gson:gson:2.2.2'
}
